!addplugindir ../altosui/Instdrv/NSIS/Plugins
!addincludedir ../altosui/Instdrv/NSIS/Includes
!include x64.nsh
!include java.nsh
!include refresh-sh.nsh

!define REG_NAME "MicroPeak"
!define PROG_ID "org.altusmetrum.micropeak.1"
!define FAT_NAME "micropeak-fat.jar"
!define ICO_ICO "micro-peak.ico"
!define ICO_EXE "micro-peak.exe"

Name "${REG_NAME} Installer"

; Default install directory
InstallDir "$PROGRAMFILES\AltusMetrum"

; Tell the installer where to re-install a new version
InstallDirRegKey HKLM "Software\${REG_NAME}" "Install_Dir"

LicenseText "GNU General Public License Version 2"
LicenseData "../COPYING"

; Need admin privs for Vista or Win7
RequestExecutionLevel admin

ShowInstDetails Show

ComponentText "${REG_NAME} Software and Driver Installer"

Function .onInit
	DetailPrint "Checking host operating system"
	${If} ${RunningX64}
		DetailPrint "Installer running on 64-bit host"
		SetRegView 64
		StrCpy $INSTDIR "$PROGRAMFILES64\AltusMetrum"
		${DisableX64FSRedirection}
	${EndIf}
FunctionEnd

Function un.onInit
	DetailPrint "Checking host operating system"
	${If} ${RunningX64}
		DetailPrint "Installer running on 64-bit host"
		SetRegView 64
		StrCpy $INSTDIR "$PROGRAMFILES64\AltusMetrum"
		${DisableX64FSRedirection}
	${EndIf}
FunctionEnd

; Pages to present

Page license
Page components
Page directory
Page instfiles

UninstPage uninstConfirm
UninstPage instfiles

; And the stuff to install

Section "FTDI USB Driver"
	SetOutPath $INSTDIR

	File "CDM20824_Setup.exe"

	StrCpy $2 "$INSTDIR\CDM20824_Setup.exe"
	ExecWait $2
SectionEnd

Section "${REG_NAME} Application"
	Call DetectJRE

	SetOutPath $INSTDIR

	File "${FAT_NAME}"
	File "altoslib_@ALTOSLIB_VERSION@.jar"
	File "altosuilib_@ALTOSUILIB_VERSION@.jar"
	File "jfreechart.jar"
	File "jcommon.jar"

	File "*.dll"

	File "../icon/${ICO_ICO}"

	CreateShortCut "$SMPROGRAMS\${REG_NAME}.lnk" "$SYSDIR\javaw.exe" "-jar ${FAT_NAME}" "$INSTDIR\${ICO_ICO}"
SectionEnd

Section "${REG_NAME} Desktop Shortcut"
	CreateShortCut "$DESKTOP\${REG_NAME}.lnk" "$INSTDIR\${FAT_NAME}"  "" "$INSTDIR\${ICO_ICO}"
SectionEnd

Section "Documentation"

	SetOutPath $INSTDIR

	File "../doc/micropeak.pdf"
SectionEnd

Section "File Associations"

	SetOutPath $INSTDIR

	File "../icon/${ICO_EXE}"

	; application elements
	
	WriteRegStr HKCR "${PROG_ID}"			""				"MicroPeak Data File"
	WriteRegStr HKCR "${PROG_ID}"			"FriendlyTypeName"		"MicroPeak Data File"
	WriteRegStr HKCR "${PROG_ID}\CurVer"		""				"${PROG_ID}"
	WriteRegStr HKCR "${PROG_ID}\DefaultIcon"	""				'"$INSTDIR\${ICO_EXE}",-101'
  WriteRegExpandStr HKCR "${PROG_ID}\shell\play\command" ""				'"%SYSTEMROOT%\System32\javaw.exe" -Djava.library.path="$INSTDIR" -jar "$INSTDIR\${FAT_NAME}" "%1"'

	; .mpd elements

	WriteRegStr HKCR ".mpd"				""				"${PROG_ID}"
	WriteRegStr HKCR ".mpd"				"PerceivedType"			"MicroPeak Data File"
	WriteRegStr HKCR ".mpd"				"Content Type"			"application/micropeak"

	WriteRegStr HKCR ".mpd\OpenWithProgids"		"${PROG_ID}"			""
	WriteRegStr HKCR ".mpd\${PROG_ID}"		""				"${REG_NAME}"

	Call RefreshShellIcons
SectionEnd
	
Section "Uninstaller"

	; Deal with the uninstaller

	${DisableX64FSRedirection}
	SetOutPath $INSTDIR

	; Write the install path to the registry
	WriteRegStr HKLM "SOFTWARE\${REG_NAME}" "Install_Dir" "$INSTDIR"

	; Write the uninstall keys for windows
	WriteRegStr HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\${REG_NAME}" "DisplayName" "${REG_NAME}"
	WriteRegStr HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\${REG_NAME}" "UninstallString" '"$INSTDIR\uninstall-${REG_NAME}.exe"'
	WriteRegStr HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\${REG_NAME}" "NoModify" "1"
	WriteRegStr HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\${REG_NAME}" "NoRepair" "1"

	WriteUninstaller "uninstall-${REG_NAME}.exe"
SectionEnd

Section "Uninstall"

	${DisableX64FSRedirection}

	DeleteRegKey   HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\${REG_NAME}"
	DeleteRegKey   HKLM "SOFTWARE\${REG_NAME}"

	DetailPrint "Delete uninstall reg entries"

	DeleteRegKey   HKCR "${PROG_ID}"

	DeleteRegKey   HKCR ".mpd\${PROG_ID}"
	DeleteRegValue HKCR ".mpd\OpenWithProgids" "${PROG_ID}"

	Delete "$INSTDIR\${FAT_NAME}"
	Delete "$INSTDIR\uninstall-${REG_NAME}.exe"

	Delete "$INSTDIR\${ICO_ICO}"
	Delete "$INSTDIR\${ICO_EXE}"

	; Remove shortcuts, if any
	Delete "$SMPROGRAMS\${REG_NAME}.lnk"
	Delete "$DESKTOP\${REG_NAME}.lnk"
	
	Call un.RefreshShellIcons
SectionEnd
